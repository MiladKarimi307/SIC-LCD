CLOCK	START	7EC9
	LDA	TIMER	
	STA	TIME
	LDCH	A
	STCH	T	# store char 'A' for AM to reg A
	LDA	ZERO	# set MODE to 0
	STA	MODE	# default mode is 0 (12h mode)
	LDA	TIME	
	COMP	ZERO	# if TIME < 0, skip
	JGT	FIND	
SWITCH	AND	SWT	# switch off the most sig-bit
	STA	TIME
	LDA	ONE	
	STA	MODE	# set MODE to 1 (24h mode)
FIND	LDA	TIME	
	DIV	SIAH	# TIME / 3600
	STA	HOUR	# store in HOUR
	MUL	SIAH	# HOUR * 3600
	STA	TEMP	# store in a temp
	LDA	TIME
	SUB	TEMP
	DIV	SIXTY
	STA	MIN	# MIN = (TIME-(HOUR*3600))/60
	LDA	MODE
	COMP	ONE
	JEQ	SKIP	# if 24h mode, skip.
	LDA	HOUR
	COMP	THRTEN
	JLT	SKIP	# if HOUR < 13, skip
	SUB	TWELVE
	STA	HOUR	# HOUR = HOUR-12
	LDCH	P
	STCH	T	# set T for PM
SKIP	LDA	ASCZRO	# reg A = 48 ('0')
	LDX	ZERO	
RESET	STCH	RESULT,X	# reset result to "0000"
	TIX	FOUR
	JLT	RESET	
	LDX	ZERO
	LDA	ZERO
	STA	TEMP	
HDIGS	LDA	HOUR
	DIV	TEN	# HOUR / 10
	ADD	ASCZRO	# (HOUR/10) + 48
	STCH	RESULT,X	# H1 (H1H2M1M2)
	TIX	ZERO
	SUB	ASCZRO
	MUL	TEN	# (HOUR/10) * 10
	STA	TEMP
	LDA	HOUR
	SUB	TEMP	# HOUR-((HOUR/10)*10)
	ADD	ASCZRO	
	STCH	RESULT,X	# H2 (H1H2M1M2)
	TIX	ZERO
	LDA	ZERO
	STA	TEMP
MDIGS	LDA	MIN
	DIV	TEN	# MIN / 10
	ADD	ASCZRO
	STCH	RESULT,X	# M1 (H1H2M1M2)
	TIX	ZERO
	SUB	ASCZRO
	MUL	TEN	# (MIN/10) * 10
	STA	TEMP
	LDA	MIN
	SUB	TEMP	# MIN-((MIN/10)*10)
	ADD	ASCZRO
	STCH	RESULT,X	# M2 (H1H2M1M2)
	LDX	ZERO
	LDA	ZERO
WROUT	LDCH	RESULT,X
TDONE	TD	OUTPUT	# test output devive
	JEQ	TDONE	# equal means not ready
	WD	OUTPUT
	TIX	FOUR
	JLT	WROUT
	LDA	MODE
	COMP	ONE
	JEQ	FIN	# if 24h format jump to FIN
	LDCH	T
TDTWO	TD	OUTPUT	# write T to the output device
	JEQ	TDTWO
	WD	OUTPUT
FIN	LDA	ZERO
SIAH	WORD	3600
ZERO	WORD	0
ONE	WORD	1
TWO	WORD	2
THREE	WORD	3
FOUR	WORD	4
TEN	WORD	10
TWELVE	WORD	12
THRTEN	WORD	13
ASCZRO	WORD	48
SIXTY	WORD	60
SWT	BYTE	X'7FFFFF'
OUTPUT	BYTE	X'40'
P	BYTE	C'P'
A	BYTE	C'A'
TEMP	RESW	1	
MODE	RESW	1
T	RESB	1
RESULT	RESB	4	#HHMM	
HOUR	RESW	1
MIN	RESW	1
TIME	RESW	1	# captured timer value at program start	
TIMER	RESW	1	#this needs to end at 7FFD
	END